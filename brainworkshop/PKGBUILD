# Contributor: Keerthan Jaic <jckeerthan at gmail dot com>
pkgname=brainworkshop
pkgver=4.8.4
pkgrel=4
pkgdesc="Brain Workshop is a free open-source version of the Dual N-Back mental exercise"
arch=('any')
url="http://brainworkshop.sourceforge.net/"
license=('GPL')
depends=('python2' 'python2-pyglet' 'avbin7' 'openal')
source=(http://downloads.sourceforge.net/brainworkshop/$pkgname-$pkgver.zip
        brainworkshop.sh
        brainworkshop.desktop)
sha256sums=('bb1f259eda90e945a803524eedbc0087283e3fb9ae2ab4c16ded88c8c4e95b15'
            '785408b4f5c6b0d3bd92d8119b3ecd2213259937b76c7da049b9f08ec0283d01'
            '1e645c18ce0648f82285da25a5bbbad1e63e282bfe9d5c31329eb9d04c23f21c')

package() {

  #Data
  _datadir="${pkgdir}/usr/share/${pkgname}"
  install -d ${_datadir}
  cp -a ${srcdir}/${pkgname}/{brainworkshop.pyw,res} ${_datadir}

  #Docs
  _docdir=${pkgdir}/usr/share/doc/${pkgname}
  install -d "${_docdir}"
  install -D -m644 "${srcdir}/${pkgname}/Readme.txt" "${_docdir}"
  install -D -m644 "${srcdir}/${pkgname}/data/Readme-stats.txt" "${_docdir}"

  #Launcher and Icon
  install -Dm75 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -D -m644 "${srcdir}/${pkgname}/res/misc/brain/brain.png" "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}

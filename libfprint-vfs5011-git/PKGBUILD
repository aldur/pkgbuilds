pkgname=libfprint-vfs5011-git
pkgver=0.5.1.13.g35e356f
pkgrel=1
pkgdesc="Library for fingerprint readers"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/fprint/libfprint"
license=(LGPL)
depends=(libusb nss gdk-pixbuf2)
makedepends=(git)
groups=(fprint)
provides=(libfprint)
conflicts=(libfprint)
options=(!libtool !emptydirs)
install=fprint.install
source=('git+git://anongit.freedesktop.org/libfprint/libfprint'
        01_vfs5011.patch::https://bugs.freedesktop.org/attachment.cgi?id=88670
        02_validity_0017.patch)
#02_validity_0017_support.patch::https://bugs.freedesktop.org/attachment.cgi?id=91917)
md5sums=('SKIP'
         '4c5b7008e55bf2947f6e8590636ed6e7'
         '6f2c498b6db0f00b0223e0b860bf29e9')
 
pkgver() {
  cd "$srcdir/libfprint"
  git describe --always | sed 's|^v_||' | sed 's|_|.|g' | sed 's|-|.|g'
}

prepare() {
  cd "$srcdir/libfprint"
  patch -p1 -i ../01_vfs5011.patch
  patch -p1 -i ../02_validity_0017.patch
}
 
build() {
  cd "$srcdir/libfprint"
  ./autogen.sh --prefix=/usr \
               --sysconfdir=/etc \
               --disable-static
  make
}
 
package() {
  cd "$srcdir/libfprint"
  make DESTDIR="$pkgdir" install
}
